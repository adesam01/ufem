!* Input file for uFEM 0.2.40 and above
!*
!* Simple 2D wall of arc shape and with compression-only supports
!*
!* File: arc.mac
!* Date: Sat Jun 28 23:02:39 CEST 2008

!* user defined script (function):
script,obloucek
csys,cylxy

for,aa,1,60,
!* node numbers:
ieval,bb,aa,-,1
ieval,cc,bb,*,4
ieval,n1,cc,+,1
ieval,n2,cc,+,2
ieval,n3,cc,+,3
ieval,n4,cc,+,4

!* angles:
eval,ang1,bb,*,3
eval,ang2,aa,*,3

printvar,n1
printvar,n2
printvar,n3
printvar,n4

!* keypoints
k,n1, 4,ang1
k,n2, 5,ang1
k,n3, 4,ang2
k,n4, 5,ang2

!*areas:
a,aa, n1,n2,n4,n3
gediv,aa,8,2

endfor

csys,cart
endscript


!* normal input file:

cleardata
jobname,test-arc

et,1,2

rs,1,2
r,height, 1, 0.01 !* thickness

mat,1,1
mp,ex,  1, 20e9
mp,prxy,1, 0.2

ddiv,2,1

!* running derined script (function):
runscript,obloucek

mesh

unzoom,3
plotprop,smallnode,yes
eplot

!* suports on sides:
nsel,s,loc,y,0
d,all,ux
!* compression-only supports:
d,all,uy-

!* force on top:
nsel,s,loc,x,5
nsel,s,loc,y,5
f,all,fy,-10e3

save

!* solve for one-directional supports:
solve,linear,20,333,1,1,yes

pldef     !* deformations
plrs      !* reactions
ples,s_x  !* normal stresses in x direction
